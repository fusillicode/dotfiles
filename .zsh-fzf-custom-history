# Zsh history and fzf
HISTSIZE=1000000             # How many lines to keep in active memory
SAVEHIST=1000000             # How many lines to keep in the actual file
setopt EXTENDED_HISTORY      # Save timestamps (This replaces HISTTIMEFORMAT)
setopt HIST_FIND_NO_DUPS     # Do not display a line previously found, even if it's a dup
setopt HIST_IGNORE_ALL_DUPS  # If a new command is a duplicate, delete the old one
setopt HIST_IGNORE_SPACE     # Don't save commands that start with a space
setopt HIST_REDUCE_BLANKS    # Remove superfluous blanks before recording
setopt INC_APPEND_HISTORY    # Write to the history file immediately, not just on exit
setopt SHARE_HISTORY         # Share history between all open terminal tabs

# Check if fzf is installed
if ! (( $+commands[fzf] )); then
  echo "Warning: fzf not found, custom history widget not loaded."
  return 1
fi

source <(fzf --zsh)
[[ -n "${terminfo[kcuu1]}" ]] && bindkey "${terminfo[kcuu1]}" fzf-history-widget

unset FZF_CTRL_R_COMMAND

fzf-custom-history() {
  local selected

  # Using custom inline fzf override.
  selected=$(h | fzf \
    --exact \
    --info=inline \
    --height=7 \
    --multi \
    --bind 'tab:accept' \
    --prompt='' \
    --separator='' \
    --pointer='' \
    --marker='+' \
    --color=hl:#8cf8f6,hl+:#8cf8f6:bold,fg+:bold \
    --query="$LBUFFER" \
    --nth=3.. \
    --no-sort \
  )

  if [[ -n "$selected" ]]; then
    # Trim leading whitespace.
    selected="${selected#"${selected%%[![:space:]]*}"}"
    # Extract command (4th field onwards) using Zsh's internal parser.
    LBUFFER="${${(z)selected}[3,-1]}"
  fi
  zle reset-prompt
}

zle -N fzf-custom-history

# Bindings ctrl-r, ctrl-p, up-arrow.
bindkey '^R' fzf-custom-history
bindkey '^P' fzf-custom-history
[[ -n "${terminfo[kcuu1]}" ]] && bindkey "${terminfo[kcuu1]}" fzf-custom-history
