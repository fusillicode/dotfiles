# History configuration
HISTFILE="$HOME/.zsh_history"
HISTSIZE=1000000
SAVEHIST=1000000
setopt EXTENDED_HISTORY       # Save timestamps
setopt HIST_FIND_NO_DUPS      # Don't display duplicates when searching
setopt HIST_IGNORE_ALL_DUPS   # Delete old duplicate entries
setopt HIST_IGNORE_SPACE      # Don't save commands starting with space
setopt HIST_REDUCE_BLANKS     # Remove superfluous blanks
setopt HIST_VERIFY            # Show expanded history before executing
setopt INC_APPEND_HISTORY     # Write immediately, not on exit
setopt SHARE_HISTORY          # Share between terminals
[[ -f "$HISTFILE" ]] && chmod 600 "$HISTFILE"

# FZF integration
if ! (( $+commands[fzf] )); then
  echo "Warning: fzf not found, custom history widget not loaded."
  return 1
fi
_fzf_cache="$HOME/.cache/zsh/fzf.zsh"
if [[ ! -f "$_fzf_cache" || "$(command -v fzf)" -nt "$_fzf_cache" ]]; then
  mkdir -p "$HOME/.cache/zsh"
  fzf --zsh > "$_fzf_cache"
fi
source "$_fzf_cache"
unset FZF_CTRL_R_COMMAND

# Custom history widget
function fzf-custom-history() {
  local selected
  selected=$(h | fzf \
    --exact \
    --info=inline \
    --height=7 \
    --multi \
    --bind 'tab:accept' \
    --prompt='' \
    --separator='' \
    --pointer='' \
    --marker='+' \
    --color='hl:#8cf8f6,hl+:#8cf8f6:bold,fg+:bold' \
    --query="$LBUFFER" \
    --nth=3.. \
    --no-sort \
  )
  if [[ -n "$selected" ]]; then
    selected="${selected#"${selected%%[![:space:]]*}"}"
    LBUFFER="${${(z)selected}[3,-1]}"
  fi
  zle reset-prompt
}
zle -N fzf-custom-history
bindkey '^R' fzf-custom-history
bindkey '^P' fzf-custom-history
bindkey '^[[A' fzf-custom-history
