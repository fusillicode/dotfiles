# Zsh history and fzf
setopt EXTENDED_HISTORY
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_ALL_DUPS
HISTSIZE=99999
SAVEHIST=99999

# Check if fzf is installed
if ! (( $+commands[fzf] )); then
  echo "Warning: fzf not found, custom history widget not loaded."
  return 1
fi

source <(fzf --zsh)
[[ -n "${terminfo[kcuu1]}" ]] && bindkey "${terminfo[kcuu1]}" fzf-history-widget

unset FZF_CTRL_R_COMMAND

fzf-custom-history() {
  local selected

  # Using custom inline fzf override.
  selected=$(h | fzf \
    --layout=reverse \
    --info=inline \
    --height=12 \
    --multi \
    --cycle \
    --bind 'tab:accept' \
    --prompt='' \
    --separator='' \
    --pointer='' \
    --marker='+' \
    --color=hl:#8cf8f6,hl+:#8cf8f6:bold,fg+:bold \
    --query="$LBUFFER" \
    --nth=3.. \
    --no-sort \
  )

  if [[ -n "$selected" ]]; then
    # Trim leading whitespace.
    selected="${selected#"${selected%%[![:space:]]*}"}"
    # Extract command (4th field onwards) using Zsh's internal parser.
    LBUFFER="${${(z)selected}[3,-1]}"
  fi
  zle reset-prompt
}

zle -N fzf-custom-history

# Bindings ctrl-r, ctrl-p, up-arrow.
bindkey '^R' fzf-custom-history
bindkey '^P' fzf-custom-history
[[ -n "${terminfo[kcuu1]}" ]] && bindkey "${terminfo[kcuu1]}" fzf-custom-history
